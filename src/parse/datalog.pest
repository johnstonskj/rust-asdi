// ------------------------------------------------------------------------------------------------
// Top-Level definitions
// ------------------------------------------------------------------------------------------------

program = {
    SOI ~ pragma* ~ ( fact | rule | query )* ~ EOI
}

// ------------------------------------------------------------------------------------------------
// Intermediate definitions
// ------------------------------------------------------------------------------------------------

fact = {
    predicate ~ (LEFT_PAREN ~ constant_list ~ RIGHT_PAREN)? ~ DOT
}

rule = {
    // FEATURE: disjunction
    atom ~ ( disjunction ~ atom )* ~ ( material_implication ~ literal_list )? ~ DOT
}

query = {
    ( query_prefix ~ atom ~ DOT )
    | ( atom ~ QMARK )
}

atom = {
    predicate ~ ( LEFT_PAREN ~ term_list ~ RIGHT_PAREN )
}

literal = {
    // FEATURE: negation
    // FEATURE: comparisons
    negation? ~ ( atom | comparison )
}

literal_list = _{
    literal ~ ( conjunction ~ literal )*
}

comparison = {
    term ~ comparison_operator ~ term ?
}

term = {
    variable | constant
}

term_list = _{
    term ~ ( COMMA ~ term )*
}

constant = {
    identifier | string | number | boolean
}

constant_list = _{
    constant ~ ( COMMA ~ constant)*
}

predicate = @{
    LOWERCASE_LETTER ~ ( CASED_LETTER | DECIMAL_NUMBER | UNDERSCORE )*
}

variable = @{
    UNDERSCORE | ( UPPERCASE_LETTER ~ ( CASED_LETTER | DECIMAL_NUMBER | UNDERSCORE )* )
}

identifier = @{
    LETTER ~ ( CASED_LETTER | DECIMAL_NUMBER | UNDERSCORE )*
}

// ------------------------------------------------------------------------------------------------
// Pragmas
// ------------------------------------------------------------------------------------------------

pragma = {
    AT_SIGN ~ (decl_relation | decl_feature | decl_include | decl_input | decl_output) ~ DOT
}

decl_relation = {
    pkw_declare ~ predicate ~ LEFT_PAREN ~ attribute_list ~ RIGHT_PAREN
}

attribute_list = _{
    attribute ~ (COMMA ~ attribute)*
}

attribute = {
    ( predicate ~ ":" )? ~ ( tid_string | tid_integer | tid_boolean )
}

decl_feature = {
    pkw_feature ~ LEFT_PAREN ~ feature_list ~ RIGHT_PAREN
}

feature_list = _{
    feature_id ~ ( COMMA ~ feature_id )*
}

feature_id = _{
    fid_negation | fid_comparisons | fid_disjunction
}

decl_include = {
    pkw_include ~ string
}

decl_input = {
    pkw_input ~ predicate ~ string
}

decl_output = {
    pkw_output ~ predicate ~ string
}

pkw_declare = _{
    "declare"
}

pkw_feature = _{
    "feature"
}

pkw_include = _{
    "include"
}

pkw_input = _{
    "input"
}

pkw_output = _{
    "output"
}

tid_string = {
    "string"
}

tid_integer = {
    "integer"
}

tid_boolean = {
    "boolean"
}

fid_negation = {
    "negation"
}

fid_comparisons = {
    "comparisons"
}

fid_disjunction = {
    "disjunction"
}

// ------------------------------------------------------------------------------------------------
// Constant definitions
// ------------------------------------------------------------------------------------------------

string = ${
    QUOTE ~ string_inner ~ QUOTE
}

string_inner = @{
    string_char*
}

string_char = {
    !( "\"" | "\\" ) ~ ANY
    | "\\" ~ ( "\"" | "\\" | "t" )
    | "\\" ~ ( "u" ~ LEFT_BRACE ~ ASCII_HEX_DIGIT{2,6} ~ RIGHT_BRACE )
}

number = @{
    ( "+" | "-" )? ~ ASCII_DIGIT+ ~ ( DOT ~ ASCII_DIGIT+ )?
}

boolean = @{
    AT_SIGN ~ ( "true" | "false" )
}

// ------------------------------------------------------------------------------------------------
// Terminal definitions
// ------------------------------------------------------------------------------------------------

material_implication = _{
    ":-" | "<-" | UC_LEFT_ARROW_FW
}

negation = {
    EXCLAMATION | UC_LOGICAL_NOT_FW | "NOT"
}

conjunction = _{
    COMMA | UC_LOGICAL_AND | AMPERSAND | "AND"
}

disjunction = {
    UC_LOGICAL_OR | VERTICAL_BAR | "OR"
}

query_prefix = _{
    "?-"
}

comparison_operator = {
    LESS_THAN | less_or_equal | GREATER_THAN | greater_or_equal | EQUALS | not_equal
}

less_or_equal = _{
    "<=" | UC_LTE
}

greater_or_equal = _{
    ">=" | UC_GTE
}

not_equal = _{
    "!=" | "/=" | UC_NOT_EQUAL
}

UC_LOGICAL_AND = _{ "⋀" }

UC_LOGICAL_OR = _{ "⋁" }

UC_LOGICAL_NOT_FW = _{ "￢" }

UC_LEFT_ARROW_FW = _{ "⟵" }

UC_LTE = _{ "≤" }

UC_GTE = _{ "≥" }

UC_NOT_EQUAL = _{ "≠" }

LEFT_PAREN = _{ "(" }

RIGHT_PAREN = _{ ")" }

LEFT_BRACE = _{ "{" }

RIGHT_BRACE = _{ "}" }

LESS_THAN = _{
    "<"
}

GREATER_THAN = _{ ">" }

EQUALS = _{ "=" }

QUOTE = _{ "\"" }

HYPHEN_MINUS = _{ "-" }

UNDERSCORE = _{ "_" }

AMPERSAND = _{ "&" }

VERTICAL_BAR = _{ "|" }

EXCLAMATION = _{ "!" }

DOT = _{ "." }

COMMA = _{ "," }

QMARK = _{ "?" }

AT_SIGN = _{ "@" }

// ------------------------------------------------------------------------------------------------
// Built-in rules
// ------------------------------------------------------------------------------------------------

COMMENT = _{ "#" ~ ( !NEWLINE ~ ANY )* }

WHITESPACE = _{ " " | "\t" | NEWLINE }